
*:/bitchslap $ = >>>
	run_cmd('/ban' $)
<<<
/*
*:/ban $player [$] = >>>
	_assertperm('mod', 0)
	if (_contains('nerd.nu/appeal', $)) {
		runas(player(), /ban $)
	} else {
		runas(player(), /ban $ 'nerd.nu/appeal')
	}
<<<
*/
*:/unban $player = >>>
	_assertperm('mod', 0)
	sudo(/unban $player)
<<<

*:/rbban $p $ = >>>
	call_alias(concat('/lbrb ', $p))
	call_alias(concat('/ban ', $p, ' ', $))
<<<


# /warncrops player [message]
#	Warn a player for crop grief by mail and add a note.
#	The player name must match exactly.
#	Additional arguments after the player name are an extra mail message.

*:/warncrops $player [$] = >>>
	_assertperm('mod')
	@server = import('sharedata.source')
	@additional = $
	run_cmd('/addnote '.$player.' warned for crop grief on '.@server)
	call_alias('/warn '.$player.' If you take crops planted by someone else, you must replant them.')
	call_alias('/warn '.$player.' That includes: potatoes, carrots, nether warts, sugar cane and cocoa.')
	msg(color(GOLD).'Warned '.$player.' for crop grief.')
	if (@additional != '') {
		call_alias('/mail send '.$player.' '.@additional)
		msg(color(GOLD).'Additional mail message:')
		msg(color(GOLD).@additional)
	}
	
<<<


# /warngrief player [message]
# 	Warn a player for general grief by mail and add a note.
#	The player name must match exactly.
# 	Additional arguments after the player name are an extra mail message.

*:/warngrief $player [$] = >>>
	_assertperm('mod')
	@server = import('sharedata.source')
	@additional = $
	run_cmd('/addnote '.$player.' warned for general grief on '.@server)
	call_alias('/warn '.$player.' You must not edit other players\' builds without permission. ')
	if (@server == 'survival') {
		call_alias('/warn '.$player.' You can break into a base to fight, but you must put back the blocks immediately.')
	}
	if (@server == 'creative') {
		call_alias('/warn '.$player.' Only staff members are allowed to clear old builds.')
	} else {
		call_alias('/warn '.$player.' Do not steal blocks. There is no such thing as an "abandoned" build.')
	}
	if (@additional != '') {
		call_alias('/warn '.$player.' '.@additional)
		msg(color(GOLD).'Additional warning:')
		msg(color(GOLD).@additional)
	}
<<<


# /warncombat player [message]
# 	Warn a player for combat logging.
#	The player name must match exactly.
# 	Additional arguments after the player name are an extra mail message.

*:/warncombat $player [$] = >>>
	_assertperm('admin')
	@server = import('sharedata.source')
	if (@server != 'survival') {
		msg(color(RED).'Combat logging on '.@server.'? Really?')
	} else {
		@additional = $
		run_cmd('/addnote '.$player.' Warned for combat logging on Survival')
		call_alias('/warn '.$player.' This is a warning for your recent combat logging.')
		call_alias('/warn '.$player.' Any abuse of login to evade or gain an edge in combat is against the rules.')
		call_alias('/warn '.$player.' Please see nerd.nu/rules and refrain from this behavior in the future.')
		if (@additional != '') {
			call_alias('/warn '.$player.' '.@additional)
			msg(color(GOLD).'Additional warning:')
			msg(color(GOLD).@additional)
		}
	}
<<<


# /bancrops player [message]
#	Ban a player for crop grief. Additional aruments are added to the ban message after a semicolon.
#	The player name must match exactly.

*:/bancrops $player [$] = >>>
	_assertperm('mod')
	@server = import('sharedata.source')
	@message = 'crop grief on '.@server
	if ($ != '') {
		@message = @message.'; '.$
	}
	call_alias('/ban '.$player.' '.@message)
<<<


# /rbbancrops player [message]
#	Ban the player for crop grief (as with /bancrops) and do a full global rollback.

*:/rbbancrops $player [$] = >>>
	call_alias(concat('/bancrops ', $player, ' ', $))
	call_alias(concat('/lbrb ', $player))
<<<


# /bangrief player [message]
#	Ban a player for general grief. Additional aruments are added to the ban message after a semicolon.
#	The player name must match exactly.

*:/bangrief $player [$] = >>>
	_assertperm('mod')
	@server = import('sharedata.source')
	@message = 'grief on '.@server
	if ($ != '') {
		@message = @message.'; '.$
	}
	call_alias('/ban '.$player.' '.@message)
<<<


# /rbbangrief player [message]
#	Ban the player for general grief (as with /bangrief) and do a full global rollback.

*:/rbbangrief $player [$] = >>>
	call_alias(concat('/bangrief ', $player, ' ', $))
	call_alias(concat('/lbrb ', $player))
<<<
